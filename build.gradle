apply from: rootProject.file('tools/predex.gradle')

buildscript {
	apply from: rootProject.file('versions.gradle')

	ext.android_plugin_version = '3.0.0-rc1'
	ext.fabricGradle = '1.22.1'

	addRepos(repositories)

	dependencies {
		classpath 'com.android.tools.build:gradle:3.0.1'
		classpath "com.google.gms:google-services:3.1.0"
		classpath "io.fabric.tools:gradle:$fabricGradle"
		classpath "net.ltgt.gradle:gradle-errorprone-plugin:0.0.13"
	}

	System.properties['com.android.build.gradle.overrideVersionCheck'] = 'true'
}

ext {
	// support
	supportLibrary = '27.0.2'
	testSupportLibrary = '0.5'
	constraintLayout = '1.0.2'

	// test dependencies
	junit = '4.12'
	hamcrest = '1.3'
	testButler = '1.3.1'
	robolectric = '3.5.1'
	espresso = '3.0.1'
	mockito = '2.10.0'
	requestmatcher = '2.2.0'
	jsonpathassert = '2.2.0'

	// main dependencies
	okhttp = '3.9.0'
	retrofit = '2.3.0'
	play = '11.6.2'
	gson = '2.8.2'
	fresco = '1.5.0'
	rxAndroid = '2.0.1'
	rxJava = '2.1.6'
	sqlbrite = '2.0.0'
	licensesDialog = '1.8.3'
	crashlytics = '2.7.1@aar'
	traceur = '1.0.1'
	dagger = '2.12'

	// utils
	leakCanary = '1.5.4'
	stetho = '1.5.0'
	googlefindbugs = '3.0.1'
	errorprone = '2.1.3'
	nullaway = '0.2.0'
}

allprojects {
	addRepos(repositories)
}

subprojects {
	apply plugin: 'net.ltgt.errorprone'

	afterEvaluate { Project project ->
		tasks.withType(JavaCompile) {
			options.compilerArgs.addAll([
				"-XepOpt:NullAway:AnnotatedPackages=com.example,br.com.vitorsalgado",
				"-XepOpt:NullAway:KnownInitializers=android.app.Activity.onCreate,android.app.Application.onCreate",
				"-XepOpt:NullAway:ExcludedFieldAnnotations=javax.inject.Inject",
				"-XepOpt:NullAway:UnannotatedSubPackages=com.example.databinding,br.com.vitorsalgado.androidstarter.databinding"
			])
		}

		if (project.findProperty('android')) {
			android {
				compileOptions {
					sourceCompatibility JavaVersion.VERSION_1_8
					targetCompatibility JavaVersion.VERSION_1_8
				}

				lintOptions {
					lintConfig file("$rootDir/lint.xml")
				}

				compileSdkVersion 27
				buildToolsVersion "27.0.1"

				defaultConfig {
					minSdkVersion 19
					targetSdkVersion 27
					versionCode 1
					versionName "1.0"
				}
			}
		}
	}
}

task clean(type: Delete) {
	delete rootProject.buildDir
}

task printBuildScriptClasspath {
	doLast {
		println project.buildscript.configurations.classpath.asPath
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '4.3.1'
	distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}
