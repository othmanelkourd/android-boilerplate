import com.github.spotbugs.SpotBugsTask

apply plugin: "org.owasp.dependencycheck"
apply plugin: "org.sonarqube"
apply plugin: "com.github.spotbugs"
apply from: "$rootDir/build.utils.gradle"

buildscript {
  ext.kotlin_version = '1.2.41'

  repositories {
    google()
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
    gradlePluginPortal()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:3.1.2"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${Versions.kotlin}"
    classpath "org.jacoco:org.jacoco.core:${Versions.jacoco}"
    classpath "com.google.gms:google-services:${Versions.google_services}"
    classpath "io.fabric.tools:gradle:${Versions.fabric}"
    classpath "org.owasp:dependency-check-gradle:${Versions.owasp_dependency_check}"
    classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:${Versions.sonar}"
    classpath "gradle.plugin.com.github.spotbugs:gradlePlugin:${Versions.spotbugs_plugin}"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:${Versions.coveralls}"
  }
}

ext.testButler = '1.3.2'
ext.java_version = JavaVersion.VERSION_1_8

allprojects {
  if (shouldSkip(it)) return

  repositories {
    google()
    jcenter()
  }

  apply plugin: 'jacoco'

  jacoco {
    toolVersion = Versions.jacoco
  }

  configurations.all {
    resolutionStrategy {
      eachDependency { details ->
        if (details.requested.group == 'com.android.support') {
          details.useVersion Versions.supportLibrary
        }
      }
    }
  }

  tasks.withType(JavaCompile) {
    sourceCompatibility = java_version
    targetCompatibility = java_version

    configure(options) {
      options.encoding = "utf-8"
      options.incremental = true
      options.fork = true
    }
  }

  tasks.withType(Test) {
    if (project.hasProperty("skipReports")) {
      reports.html.enabled = false
      reports.junitXml.enabled = false
    }

    jacoco.includeNoLocationClasses = true

    testLogging {
      exceptionFormat "full"
      showExceptions true
      showCauses true
      showStackTraces true
      maxParallelForks BuildUtils.cores
      forkEvery 10
      events = ["failed", "passed", "skipped", "standardOut", "standardError"]
    }
  }

  gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
      options.compilerArgs << "-Xlint:deprecation"
    }
  }

  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
      jvmTarget = java_version
    }
  }
}

subprojects {
  if (shouldSkip(it)) return

  setPreDex(it)

  apply from: rootProject.file('tools/linters.gradle')
  apply from: rootProject.file('tools/coverage.gradle')


  afterEvaluate { Project project ->
    project.tasks.whenTaskAdded {
      if (it.name == "testReleaseUnitTest") {
        it.enabled = false
      }
    }

    if (isAndroidModule(project)) {
      android {
        compileOptions {
          sourceCompatibility JavaVersion.VERSION_1_8
          targetCompatibility JavaVersion.VERSION_1_8
        }

        compileSdkVersion Android.target_sdk

        defaultConfig {
          minSdkVersion Android.min_sdk
          targetSdkVersion Android.target_sdk
          versionCode 1
          versionName "1.0"
          vectorDrawables.useSupportLibrary true
        }

        sourceSets {
          main.java.srcDirs += 'src/main/kotlin'
          debug.java.srcDirs += 'src/debug/kotlin'
          release.java.srcDirs += 'src/release/kotlin'
          test.java.srcDirs += 'src/test/kotlin'
          androidTest.java.srcDirs += 'src/androidTest/kotlin'
        }
      }
    }

    tasks.withType(SpotBugsTask) {
      excludeFilter rootProject.file("tools/linters/spotbugs-filter.xml")
      reports {
        xml.enabled = false
        html.enabled = true
        html.setDestination rootProject.file("reports/spotbugs/index.html")
      }
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.7'
  distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}
