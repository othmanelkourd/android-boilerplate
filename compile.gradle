final cores = Runtime.runtime.availableProcessors().intdiv(2) ?: 1

tasks.withType(JavaCompile) {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8

	configure(options) {
		encoding = "utf-8"
		incremental = true
		fork = true
	}
}

tasks.withType(Test) {
	if (project.hasProperty("skipReports")) {
		reports.html.enabled = false
		reports.junitXml.enabled = false
	}

	testLogging {
		exceptionFormat "full"
		showExceptions true
		showCauses true
		showStackTraces true
		maxParallelForks cores
		forkEvery 10
		events = ["failed", "passed", "skipped", "standardOut", "standardError"]
	}
}

tasks.withType(JavaForkOptions) {
	jvmArgs "-XX:+UseCompressedOops"
	jvmArgs "-Djava.awt.headless=true"
	jvmArgs "-Dapple.awt.UIElement=true"
}

configurations.all {
	resolutionStrategy {
		force "org.hamcrest:hamcrest-core:$versions.hamcrest"
		force "com.google.code.findbugs:jsr305:$versions.googlefindbugs"
	}

	resolutionStrategy.eachDependency { details ->
		if (details.requested.group == 'com.android.support'
			&& !details.requested.name.contains('multidex')) {
			details.useVersion "$versions.supportLibrary"
		}
	}
}
