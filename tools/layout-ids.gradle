import java.util.regex.Pattern

import static java.util.regex.Pattern.compile

task listLayoutIDs {
  group 'utils'
  description 'Print all IDs contained in Android layout files'

  doLast {
    final Pattern VIEW = compile("<([\\w.]+)")
    final Pattern ID = compile("android:id=\"(.*?)\"")

    def layoutDir = "${project.projectDir.absolutePath}/src/main/res/layout"

    println "LayoutDir = $layoutDir"

    def files = new File(layoutDir).listFiles()

    files.each {
      println "\n${it.getName()}"
      String viewType

      it.newReader().lines().each {
        def viewMatcher = VIEW.matcher(it)

        if (viewMatcher.find())
          viewType = viewMatcher.group(1)

        def idMatcher = ID.matcher(it)

        if (idMatcher.find())
          println "\t${idMatcher.group(1)} -> $viewType"
      }
    }
  }
}
