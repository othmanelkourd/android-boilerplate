def lintersDir = "$rootDir/tools/linters"
def reportsDir = "$rootDir/reports"

afterEvaluate { project ->
	apply plugin: 'pmd'
	apply from: rootProject.file('tools/exclusions.gradle')

	pmd {
		toolVersion = "5.8.1"
	}

	task pmd(type: Pmd) {
		group "QA"
		ruleSetFiles = files("$lintersDir/pmd-ruleset.xml")
		ruleSets = []

		source 'src/main/java'
		include '**/*.java'
		exclude '**/gen/**'

		reports {
			xml.enabled = false
			html.enabled = true
			xml.setDestination(file("$reportsDir/pmd/pmd.xml"))
			html.setDestination(file("$reportsDir/pmd/index.html"))
		}
	}

	if (project.findProperty('android')) {
		apply plugin: 'checkstyle'

		checkstyle {
			toolVersion "8.4"
		}

		task checkstyle(type: Checkstyle) {
			group "Linters"
			configFile file("$lintersDir/checkstyle.xml")
			showViolations true

			source "src/main/java"
			classpath = files()
		}
	}

	def task = tasks.findByName("check")

	if (task != null) {
		if (project.findProperty('android')) {
			task.dependsOn("pmd", "checkstyle")
		} else {
			task.dependsOn("pmd")
		}
	}
}
